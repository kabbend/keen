 
/* md file syntax: 
 * i)    C-style comments are supported and ignored by parser
 * ii)   md file describes patterns for instruction selection phase, for a particular architecture. 
 *       Each pattern is called a rule and is written using following grammar:
 *         "clause { list-of-instructions }"
 *       where clause is a (recursive) description of an irnode tree, and an instruction is of the form
 *         "insn_type (list-of-temp-dest) (list-of-temp-source) (list-of-jumps) asm-template | asm-function-call() ;"
 *         or
 *         "insn-function-call() ;"
 *   a)  asm-template is a C-string that represents assembly code or pseudo-assembly code (in particular because
 *       actual hardware registers are not known at that time). String is used as-is as input for assembler pass, 
 *       except for some escape sequences described in note (vii)
 *   b)  asm-function-call can be used instead of a C-string. In such case the function must return a (char *)
 *   c)  a rule-body can be made only of one insn-function-call(). In this case the function must return an
 *       (irnodeList) that is inserted without modification to current instructions flow. The function receives as
 *       arguments the irnode matching the rule, and the rule number (for insertion in resulting irnode)
 * iii)  At runtime (during instruction selection phase), rules are checked in the order they appear, 
 *       so the finer the rule the earlier. For each irnode tree that needs to be translated to assembly code,
 *       we try to match with one rule clause. If one matches the irnode, the list of instructions is generated,
 *       eventually using the actual values and members stored in the irnode
 * iv)   A clause can contain variables, by using identifiers of the form "$identifier" instead of an irnode name. 
 *  a)   Variables are evaluated before any other operation is performed on the rule, i.e. the corresponding irnode
 *       is matched against rules to generate instructions if needed. Variables are evaluated in the order they appear.
 *  b)   They can be referenced in instructions dest, src and label lists afterwards. Each such reference represents 
 *       the result (a temporary) of previous evaluation, by construction the first temp in dest-list of such evaluation.
 *       It is not correct to reference a variable in instruction-lists when prior evaluation has an empty dest-list,
 *       as this would lead to runtime error.
 * v)    A clause can contain the wildcard * (star). This is just a syntactic way to complete clauses when no particular 
 *       treatment (evaluation) or matching is required
 * vi)   An instruction is made of a instruction-type (INSN_x), 3 lists in parentheses dest list, source list and 
 *       label list, representing respectively the target temporaries of the instruction, the source temporaries, 
 *       and the labels that the instruction may potentially jump to.
 *  a)   The last field of an instruction is either a string pattern, representing the assembly source code to generate,
 *       or the name of a C function to call (followed by parenthesis) when such generation is more complex and cannot
 *       be easily expressed by a string. In such case, the function receives the irnode matching the rule and must 
 *       return a (char *) string
 *  b)   Each list is a comma-separated list of
 *       - variables ($identifier), as explained in (iv-b)
 *       - nominative registers when such registers are known in instruction phase. They must begin with % character
 *         and match the hardregset[] names defined for the architecture ( ex: %eax )
 *       - C function-call, followed by (empty) parenthesis, when temporary is more complex to determine and requires
 *         additional code. In such case the return value of the called-function must be (temp *), the argument passed
 *         to the function is the (irnode *) matching the rule clause.
 *         Several basic functions are predefined:
 *         n() creates a new temporary (with the DEFAULT_MODE defined for the architecture)
 *         n1B(), n2B(), n4B() create a new temporary with given mode
 *         nopt() create a new temporary with default mode, and that can be optimized by kburg if, when used as result
 *	     by a calling rule, is source of a move instruction. 
 *       - affectation of one of the previous element. This is needed when a temporary used in one instruction of the 
 *         rule needs to be referenced again in later instruction. Syntax for such affectation is
 *          "$identifier = ( $variable | function() | %register )"
 * vii)  String template format of an instruction is described below:
 *       hereunder "L" represents a list of digits describing hierarchy within irnode tree (ex: "01" means the child 
 *       number #1 of the child number #0 of the current node). "x" and "y" represent a digit 0 to 9 
 *        "%iL" expand to the integer value of the L target
 *        "%IL" expand to the integer value of the L target, provided this integer is not 0 
 *             (otherwise expand to empty string)
 *        "%tL" expand to the temporary name of the L target
 *        "%nL" expand to the label name of the L target
 *        "%dx" expand to the temporary name of the x-th dest list member 
 *        "%d(8)x", "%d(16)x", "%d(32)x" expand, when the temporary is finally associated with an actual register, to the
 *					 sub-register with appropriate mode. For instance, "%d(8)" on "%eax" register will expand
 *					 to "%al" register. If several sub-registers are available, take the 1st one encountered
 * 					 in hardregset[] in the order they appear.
 *					 FIXME: This should be completed with appropriate register-class selection, to ensure that
 *					 such sub-registers actually exist when the temporary is colored. Without such mechanism
 *					 we today only use this feature with fixed (precolored) temporaries.
 *        "%sx" expand to the temporary name of the x-th src list member (and same sub-register method as above) 
 *        "%lx" expand to the label name of the x-th label list member
 */

MEM:m8 ( ADD( $a , CONST ) )
{ 
	INSN_OPER (nopt()) ($a) () 	"movsx %I010(%s0), %d0" ;
}	

MEM:m16 ( ADD( $a , CONST ) )
{ 
	INSN_OPER (nopt()) ($a) () 	"movsx %I010(%s0), %d0" ;
}	

MEM ( ADD( $a , CONST ) )
{ 
	INSN_OPER (nopt()) ($a) () 	"movl %I010(%s0), %d0" ;
}	

MEM:m8 ( ADD( CONST , $a ) )
{
	INSN_OPER (nopt()) ($a) () 	"movsx %I000(%s0), %d0" ;
}	

MEM:m16 ( ADD( CONST , $a ) )
{
	INSN_OPER (nopt()) ($a) () 	"movsx %I000(%s0), %d0" ;
}	

MEM ( ADD( CONST , $a ) )
{
	INSN_OPER (nopt()) ($a) () 	"movl %I000(%s0), %d0" ;
}	

MEM ( NAME )
{
	INSN_OPER (nopt()) () ()	"movl $%n00, %d0" ;
}

MEM:m8 ( $a ) 
{
	INSN_OPER (nopt()) ($a) ()       "movsx (%s0), %d0" ;  

}	

MEM:m16 ( $a ) 
{
	INSN_OPER (nopt()) ($a) ()       "movsx (%s0), %d0" ; 
}	

MEM ( $a ) 
{
	INSN_OPER (nopt()) ($a) ()       "movl (%s0), %d0" ; 
}	

CONST	
{ 
	INSN_OPER (nopt()) () () "movl $%i0, %d0" ;
}

LABEL
{
	INSN_LABEL () () (x86_gen_label()) "%l0:" ;
}

ADD( $a , CONST 0 )
{ 
	internal ($a) () () ;
}

ADD( $a , CONST )
{
	INSN_MOVE ($b = n()) ($a) ()     "movl %s0, %d0"  ;
	INSN_OPER ($b) ($b) ()           "addl $%i10, %s0" ;
}

SUB( $a , CONST )
{ 
	INSN_MOVE ($b = n()) ($a) ()     "movl %s0, %d0"  ;
	INSN_OPER ($b) ($b) ()           "subl $%i10, %s0" ;
}

ADD( $a , $b )
{ 
	INSN_MOVE ($c = n()) ($a) ()     "movl %s0, %d0" ;
	INSN_OPER ($c) ($b,$c) ()        "addl %s0, %d0" ;
}

SUB( $a , $b )
{ 
	INSN_MOVE ($c = n()) ($a) ()      "movl %s0, %d0" ;
	INSN_OPER ($c) ($b,$c) ()         "subl %s0, %d0" ;
}

/* empty but not superfluous: set use and def 
   appropriately for calling rule */
TEMP
{ 
     	internal (x86_gen_temp()) () ()  ;  
}
 
JUMP( NAME, * )
{ 
	INSN_JUMP () () (x86_gen_jump_unique_name()) "jmp %l0" ;
}

MOVE( MEM:m32 ( ADD ( $a , CONST ) ) , $b )
{ 
	INSN_OPER () ($a,$b) () "movl %s1, %I0010(%s0)" ;
}

MOVE( MEM:m16 ( ADD ( $a , CONST ) ) , $b )
{ 
	INSN_MOVE (%eax) ($b) ()      "mov %s0, %d0" ;
	INSN_OPER () (%eax,$a) ()     "movw %s(16)0, %I0010(%s1)" ;
}

MOVE( MEM:m8 ( ADD ( $a , CONST ) ) , $b )
{ 
	INSN_MOVE (%eax) ($b) ()      "mov %s0, %d0" ;
	INSN_OPER () (%eax,$a) ()     "movb %s(8)0, %I0010(%s1)" ;
}

MOVE( TEMP , MEM ( ADD ( ADD ( $a, CONST ) , CONST ) ) )
{ 
	INSN_OPER (x86_move_temp()) ($a) () 		"movl %i10010+%i1010(%s0), %d0" ;
}

MOVE( TEMP , MEM ( ADD ( $a , CONST ) ) )
{ 
	INSN_OPER (x86_move_temp()) ($a) () 		"movl %I1010(%s0), %d0" ;
}

MOVE( TEMP, ADD ( $a , CONST ) )
{
	INSN_MOVE (x86_move_temp()) ($a) () 		"movl %s0, %d0" ;
	INSN_OPER (x86_move_temp()) () () 		"addl $%i110, %d0" ;
}

MOVE( MEM ( TEMP ) , $b )
{ 
	INSN_OPER () ($b,x86_move_mem_temp()) () 	"movl %s0, (%s1)" ;
}

MOVE( MEM ( MEM ( NAME ) ) , $a )
{
	INSN_OPER ($r = n()) () ()    "movl $%n0000, %d0" ;
	INSN_OPER () ($r,$a) ()       "movl %s1, (%s0)" ;
}

MOVE( MEM:m8 ( $a ) , $b) 
{ 
	INSN_MOVE (%eax) ($b) ()      "mov %s0, %d0" ;
	INSN_OPER () (%eax,$a) ()     "movb %s(8)0, (%s1)" ;
}

MOVE( MEM:m16 ( $a ) , $b) 
{ 
	INSN_MOVE (%eax) ($b) ()      "mov %s0, %d0" ;
	INSN_OPER () (%eax,$a) ()     "movb %s(16)0, (%s1)" ;
}

MOVE( MEM ( $a ) , $b) 
{ 
	INSN_OPER () ($a,$b) ()       "movl %s1, (%s0)" ;
}

MOVE( $a, $b )
{ 
	INSN_MOVE ($a) ($b) ()   "movl %s0, %d0" ;
}

CJUMP( EQ( NE( $a, $b) , CONST 0 ), * , * )
{
	INSN_OPER  () ($a, $b) ()                 "cmpl %s1, %s0" ;
	INSN_CJUMP () () (x86_get_false_label())  "jne %l0" ;
}

CJUMP( EQ( NE( $a, $b) , CONST 1 ), * , * )
{
	INSN_OPER  () ($a, $b) ()                 "cmpl %s1, %s0" ;
	INSN_CJUMP () () (x86_get_false_label())  "je %l0" ;
}

CJUMP( NE( EQ( $a, $b) , CONST 0 ), * , * )
{
	INSN_OPER  () ($a, $b) ()                 "cmpl %s1, %s0" ;
	INSN_CJUMP () () (x86_get_false_label())  "jne %l0" ;
}

CJUMP( NE( EQ( $a, $b) , CONST 1 ), * , * )
{
	INSN_OPER  () ($a, $b) ()                 "cmpl %s1, %s0" ;
	INSN_CJUMP () () (x86_get_false_label())  "je %l0" ;
}

CJUMP( EQ( $a, $b ) , * , * )
{
	INSN_OPER  () ($a, $b) ()                 "cmpl %s1, %s0" ;
	INSN_CJUMP () () (x86_get_false_label())  "jne %l0" ;
}

CJUMP( NE( $a, $b ) , * , * )
{ 
	INSN_OPER  () ($a, $b) ()                 "cmpl %s1, %s0" ;
	INSN_CJUMP () () (x86_get_false_label())  "je %l0" ;
}

EQ( $a, $b )
{ 
	INSN_OPER () ($a,$b) ()            "cmpl %s1, %s0" ;
      	INSN_OPER ($r = n()) () ()         "sete %d(8)0" ;
      	INSN_OPER (nopt()) ($r) ()            "movzbl %s(8)0, %d0" ;
}
	 
NE( $a, $b )
{ 
	INSN_OPER () ($a,$b) ()             "cmpl %s1, %s0" ;
	INSN_OPER ($r = n()) () ()          "setne %d(8)0" ;
	INSN_OPER (nopt()) ($r) ()             "movzbl %s(8)0, %d0" ;
}	 

GT( $a, $b )
{ 
	INSN_OPER () ($a,$b) ()             "cmpl %s1, %s0" ;
	INSN_OPER ($r = n()) () ()          "setg %d(8)0" ;
	INSN_OPER (nopt()) ($r) ()             "movzbl %s(8)0, %d0" ;
}	 

GE( $a, $b )
{ 
	INSN_OPER () ($a,$b) ()             "cmpl %s1, %s0" ;
	INSN_OPER ($r = n()) () ()          "setge %d(8)0" ;
	INSN_OPER (nopt()) ($r) ()             "movzbl %s(8)0, %d0" ;
}	 

LE( $a, $b ) 
{ 
	INSN_OPER () ($a,$b) () 	    "cmpl %s1, %s0" ;
	INSN_OPER ($r = n()) () () 	    "setle %d(8)0" ;
	INSN_OPER (nopt()) ($r) () 	    "movzbl %s(8)0, %d0" ;
}	 

LT( $a, $b ) 
{ 
	INSN_OPER () ($a,$b) () 		"cmpl %s1, %s0"  ;
	INSN_OPER ($r = n()) () () 		"setl %d(8)0" ;
	INSN_OPER (nopt()) ($r) () 		"movzbl %s(8)0, %d0" ;
}	 

AND( $a, $b ) 
{ 
	INSN_MOVE ($r = n()) ($a) () 		"movl %s0, %d0" ;
	INSN_OPER ($r) ($r,$b) () 		"and %s1, %s0" ;
}	 

OR( $a, $b )
{ 
	INSN_MOVE ($r = n()) ($a) () 		"movl %s0, %d0" ;
	INSN_OPER ($r) ($r,$b) () 		"or %s1, %s0" ;
}	 

MUL( $a, $b )
{
        INSN_MOVE ($r = n()) ($a) () 		"movl %s0, %d0" ;
	INSN_OPER ($r) ($r,$b) () 		"imul %s1, %s0" ;

}	 

DIV( $a, $b )
{
      	INSN_OPER (%edx) () ()        "xor %d0,%d0" ;
      	INSN_MOVE (%eax) ($a) ()      "movl %s0, %d0" ;
	INSN_OPER (%eax) ($b,%edx,%eax) () "idiv %s0"  /* edx is lost */ ;
}

MOD( $a, $b )
{
      	INSN_OPER (%edx) () ()                  "xor %d0,%d0" ;
      	INSN_MOVE (%eax) ($a) ()                "movl %s0, %d0" ;
	INSN_OPER (%edx,%eax) ($b,%eax,%edx) () "idiv %s0"  /* edx contains modulo */ ;
}

EXPR( CALL )
{
	x86_gen_call(); 
}

CALL
{
	x86_gen_call_assign() ;
}

GLOBAL
{
	INSN_ASMD () () ()		".text" ;
	INSN_ASMD () () () 		x86_gen_func_globl() ;
}

PROLOGUE
{
	INSN_OPER () (%ebp) ()              "pushl %s0" ;
	INSN_MOVE (%ebp) (%esp) ()          "movl %s0, %d0" ;
	x86_gen_frame_stack() ;
	INSN_OPER (%ebx,%edi,%esi) () ()    "# end of prologue" ; 
        x86_set_callee_save() ;
	INSN_OPER () () ()                  "# end callee save" ;
}

EPILOGUE
{
	INSN_OPER () () ()                  "# start callee save" ;
        x86_get_callee_save() ;
	INSN_OPER () (%ebx,%edi,%esi) ()    "# epilogue " ; 
	INSN_MOVE (%esp) (%ebp) ()          "movl %s0, %d0" ;
	INSN_OPER (%ebp) () ()              "popl %d0" ;
	INSN_RET  () () () 		    "ret" ;
}

DATA
{
	INSN_ASMD () () ()		x86_gen_data_section();
	INSN_ASML () () ()		x86_gen_data_entry();
	INSN_RET  () () ()		"" ;
}

INFO
{
	INSN_ASML () () ()		x86_gen_info();
}

COPY ( $dest, $src, *)
{
	INSN_OPER () () ()              "# call copy operation" ;
	INSN_MOVE (%edi) ($dest) ()     "movl %s0,%d0" ;
	INSN_MOVE (%esi) ($src) ()      "movl %s0,%d0" ;
	INSN_OPER (%ecx) () ()          "movl $%i2,%d0" ;
	INSN_OPER (%ecx,%esi,%edi) (%edi,%esi,%ecx) () "rep movsl" ;
}

BOR ( $a, $b )
{
	INSN_MOVE ($r = n()) ($a) ()	 "movl %s0,%d0" ;
	INSN_OPER ($r) ($b) ()           "orl %s0,%d0" ;
}

BAND ( $a, $b )
{
	INSN_MOVE ($r = n()) ($a) ()	 "movl %s0,%d0" ;
	INSN_OPER ($r) ($b) ()           "andl %s0,%d0" ;
}

/* signed shift */
LSHIFT ( $a, CONST )
{
	INSN_MOVE ($r = n()) ($a) ()     "movl %s0, %d0" ;
	INSN_OPER ($r) ($r) ()           "sall $%i10, %d0" ;
}

/* signed shift */
LSHIFT ( $a, $b )
{
	INSN_MOVE (%ecx) ($b) ()          "movl %s0, %d0" ;
	INSN_OPER ($r = n()) (%ecx,$a) () "sall %s(8)0, %d0" ;
}

/* signed shift */
RSHIFT ( $a, CONST )
{
	INSN_MOVE ($r = n()) ($a) ()     "movl %s0, %d0" ;
	INSN_OPER ($r) ($r) ()           "sarl $%i10, %d0" ;
}

/* signed shift */
RSHIFT ( $a, $b )
{
	INSN_MOVE (%ecx) ($b) ()           "movl %s0, %d0" ;
	INSN_OPER ($r = n()) (%ecx, $a) () "sarl %s(8)0, %d0" ;
}

BXOR ( $a, $b )
{
	INSN_MOVE ($r = n()) ($a) ()	 "movl %s0,%d0" ;
	INSN_OPER ($r) ($b,$r) ()        "xorl %s0,%d0" ;
}

/* ones complement */
COMP ( $a )
{
	INSN_MOVE ($r = n()) ($a) ()	"movl %s0,%d0" ;
	INSN_OPER ($r) ($r) ()		"notl %s0" ;
}
